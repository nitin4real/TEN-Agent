#
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0.
# See the LICENSE file for more information.
#
import("//build/feature/ten_package.gni")
import("//build/ten_runtime/feature/publish.gni")
import("//build/ten_runtime/glob.gni")
import("//build/ten_runtime/options.gni")

ten_package("event_bus_go") {
  package_kind = "extension"

  resources = [
    "LICENSE",
    "event_bus_extension.go",
    "go_release.mod=>go.mod",
    "manifest.json",
    "property.json",
  ]

  tests_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                            [
                              "--dir",
                              rebase_path("tests/**/*"),
                              "--dir-base",
                              rebase_path("tests"),
                              "--recursive",
                              "--only-output-file",
                            ],
                            "json")

  foreach(tests_file, tests_files) {
    tests_file_rel_path = tests_file.relative_path
    resources +=
        [ "tests/${tests_file_rel_path}=>tests/${tests_file_rel_path}" ]
  }

  deps = [ "//core/src/ten_runtime" ]
}

if (ten_enable_ten_manager) {
  ten_package_publish("upload_event_bus_go_to_server") {
    base_dir =
        rebase_path("${root_out_dir}/ten_packages/extension/event_bus_go")
    deps = [ ":event_bus_go" ]
  }
}
