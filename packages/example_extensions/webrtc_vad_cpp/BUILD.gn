#
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0.
# See the LICENSE file for more information.
#
import("//build/feature/ten_package.gni")
import("//build/ten_runtime/feature/publish.gni")
import("//build/ten_runtime/glob.gni")
import("//build/ten_runtime/options.gni")

ten_package("webrtc_vad_cpp") {
  package_kind = "extension"

  resources = [
    "BUILD_release.gn=>BUILD.gn",
    "LICENSE",
    "manifest.json",
    "property.json",
    "src/main.cc",
  ]

  # Include third_party webrtc_vad files
  vad_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                          [
                            "--dir",
                            rebase_path("third_party/**/*"),
                            "--dir-base",
                            rebase_path("third_party"),
                            "--recursive",
                            "--only-output-file",
                          ],
                          "json")

  foreach(vad_file, vad_files) {
    vad_file_rel_path = vad_file.relative_path
    resources +=
        [ "third_party/${vad_file_rel_path}=>third_party/${vad_file_rel_path}" ]
  }

  # Include tests files
  tests_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                            [
                              "--dir",
                              rebase_path("tests/**/*"),
                              "--dir-base",
                              rebase_path("tests"),
                              "--recursive",
                              "--only-output-file",
                            ],
                            "json")

  foreach(tests_file, tests_files) {
    tests_file_rel_path = tests_file.relative_path
    resources +=
        [ "tests/${tests_file_rel_path}=>tests/${tests_file_rel_path}" ]
  }

  docs_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                           [
                             "--dir",
                             rebase_path("docs/**/*"),
                             "--dir-base",
                             rebase_path("docs"),
                             "--recursive",
                             "--only-output-file",
                           ],
                           "json")

  foreach(docs_file, docs_files) {
    docs_file_rel_path = docs_file.relative_path
    resources += [ "docs/${docs_file_rel_path}=>docs/${docs_file_rel_path}" ]
  }

  sources = [
    "src/main.cc",
    "third_party/webrtc_vad/webrtc_vad.c",
  ]

  enable_build = true
  include_dirs = [
    "//core/src",
    "//core",
    "third_party/webrtc_vad",
  ]
  deps = [
    "//core/src/ten_runtime",
    "//third_party/nlohmann_json",
  ]
}

if (ten_enable_ten_manager) {
  ten_package_publish("upload_webrtc_vad_cpp_to_server") {
    base_dir =
        rebase_path("${root_out_dir}/ten_packages/extension/webrtc_vad_cpp")
    deps = [ ":webrtc_vad_cpp" ]
  }
}
