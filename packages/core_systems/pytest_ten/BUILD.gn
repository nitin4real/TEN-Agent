#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/feature/ten_package.gni")
import("//build/ten_runtime/feature/publish.gni")
import("//build/ten_runtime/glob.gni")
import("//build/ten_runtime/options.gni")

ten_package("pytest_ten") {
  package_kind = "system"

  resources = [
    "LICENSE",
    "manifest.json",
    "pyproject.toml",
    "pytest_ten/__init__.py",
    "pytest_ten/ten_test.py",
  ]

  docs_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                           [
                             "--dir",
                             rebase_path("docs/**/*"),
                             "--dir-base",
                             rebase_path("docs"),
                             "--recursive",
                             "--only-output-file",
                           ],
                           "json")

  foreach(docs_file, docs_files) {
    docs_file_rel_path = docs_file.relative_path
    resources += [ "docs/${docs_file_rel_path}=>docs/${docs_file_rel_path}" ]
  }

  deps = [
    "//core/src/ten_runtime",
    "//core/src/ten_runtime/binding/python",
  ]
}

if (ten_enable_ten_manager) {
  ten_package_publish("upload_pytest_ten_to_server") {
    base_dir = rebase_path("${root_out_dir}/ten_packages/system/pytest_ten")
    deps = [ ":pytest_ten" ]
  }
}
